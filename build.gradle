import net.neoforged.gradleutils.PomUtilsExtension.License

plugins {
    id 'eclipse'
    id 'signing'
    id 'java-library'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'net.neoforged.gradleutils' version '3.0.0-alpha.12'
}
apply plugin: 'net.neoforged.gradleutils'

group 'net.neoforged'

gradleutils {
    version {
        branches {
            suffixBranch = true
        }
    }
    setupCentralPublishing()
    setupSigning(signAllPublications: true)
}

version = gradleutils.version
println('Version: ' + version)

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
compileJava.options.encoding = 'UTF-8'

ext {
    TITLE = 'Auto Renaming Tool'
    MANIFEST = manifest{
        attributes('Main-Class': 'net.minecraftforge.fart.Main')
        attributes([
            'Specification-Title':      'AutoRenamingTool',
            'Specification-Vendor':     'Neo Forged',
            'Specification-Version':    gradleutils.gitInfo.tag,
            'Implementation-Title':     'ART',
            'Implementation-Version':   project.version,
            'Implementation-Vendor':    'NeoForged'
        ] as LinkedHashMap, 'net/minecraftforge/fart/')
    }
    INITIAL_CHANGELOG_TAG = '1.0'
}

repositories {
    mavenCentral()
    maven { url = 'https://maven.neoforged.net/releases' }
}

test {
    useJUnitPlatform()
}

license {
    header = file('LICENSE-header.txt')
    ext.project = TITLE
}

dependencies {
    implementation 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'
    api 'net.minecraftforge:srgutils:0.5.1'
    api 'net.neoforged.installertools:cli-utils:2.1.2'
    implementation 'org.ow2.asm:asm:9.5'
    implementation 'org.ow2.asm:asm-commons:9.5'
    implementation 'org.ow2.asm:asm-tree:9.5'
    implementation 'net.neoforged.javadoctor:gson-io:2.0.0'

    testImplementation(platform('org.junit:junit-bom:5.8.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.powermock:powermock-core:2.0.9')

    compileOnly 'org.jetbrains:annotations:24.0.1'
}


test {
  useJUnitPlatform()
  testLogging {
    events 'passed', 'skipped', 'failed'
  }
}

java.withSourcesJar()
java.withJavadocJar()

shadowJar {
    manifest.from(MANIFEST)
    minimize()

    def relocations = [
            'org.objectweb.asm',
            'net.minecraftforge.srgutils',
            'joptsimple',
            'net.neoforged.cliutils'
    ]

    relocations.forEach {
        relocate it, "net.minecraftforge.fart.relocated.$it"
    }
}

assemble.dependsOn shadowJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = TITLE
                description = 'A tool that renames java bytecode elements.'
                pomUtils.neoForgedDeveloper(it)
                pomUtils.license(it, License.LGPL_v2)
                pomUtils.githubRepo(it, 'AutoRenamingTool')
            }
        }
    }
    repositories {
        maven gradleutils.publishingMaven
    }
}

changelog {
    from INITIAL_CHANGELOG_TAG
}
